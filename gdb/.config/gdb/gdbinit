define init-pwndbg
source ~/.config/gdb/REPO-pwndbg/gdbinit.py
set context-clear-screen on
set follow-fork-mode parent

source ~/.config/gdb/REPO-splitmind/gdbinit.py
python
import splitmind
(splitmind.Mind()
  .tell_splitter(show_titles=True)
  .tell_splitter(set_title="Main")
  .right(display="backtrace", size="25%")
  .show("expressions", on="backtrace")
  .show("args", on="backtrace")
  .above(of="main", display="disasm", size="80%")
  # .show("ghidra", on="disasm")
  .right(cmd="tty; tail -f /dev/null", size="65%", clearing=False)
  .tell_splitter(set_title="Input / Output")
  .above(display="stack", size="80%", banner="none")
  .above(display="legend", size="25")
  .show("regs", on="legend")
  .below(of="backtrace", cmd="ipython", size="30%")
).build()
end
set context-code-lines 30
set context-source-code-lines 30
set context-sections  "regs args code disasm stack backtrace"
end
document init-pwndbg
Initializes PwnDBG
end

define init-gef
source ~/.config/gdb/REPO-gef/gef.py
end
document init-gef
Initializes GEF (GDB Enhanced Features)
end
